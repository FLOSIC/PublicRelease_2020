! UTEP Electronic Structure Lab (2020)
!
! ****************************************************************
!
SUBROUTINE READWF_FRAG(FAILED)
use for_diag1,only : AHAM,AOVER,IIREP,IISPN
use hstor1,only : HSTOR
use debug1
use common2,only : WFFILE, NSPN
use common3,only : RMAT
use common5,only : PSI_COEF, OCCUPANCY, N_OCC, PSI, NWF, NWFS, EVLOCC, ISTSCF
use common8,only : REP, N_REP, IGEN, NS_TOT
use fragment,only : NFRAGMENT,FRAGMENTS
! Conversion to implicit none.  Raja Zope Sun Aug 20 10:02:06 MDT 2017

!      INCLUDE  'PARAMAS'  
       INCLUDE  'PARAMA2'  
!implicit none
!
! READWF READS IN WAVEFUNCTIONS TO ALLOW RESTARTING A CALCULATION 
! AT A SELF-CONSISTENT END POINT. THIS IS CONVENIENT FOR FINE-TUNING 
! A SET OF FIT GAUSSIANS AND FOR GETTING GOOD NEW STARTING POINTS.
! IN ORDER TO MAKE SURE THE FILE IS COMPATIBLE, SOME CRITICAL 
! VARIABLES ARE COMPARED TO THE CURRENT CALCULATION. 
! IMPORTANT: IN ORDER TO HAVE THE CORRECT NS_TOT AVAILABLE, OVERLAP
! MUST HAVE BEEN CALLED BEFORE.
!
LOGICAL :: FAILED,EXIST
CHARACTER*12 :: EVALSTR,WFFILE2
CHARACTER*4  :: FRAGSTR
INTEGER :: ISPN,IREP,ITOT,IWF,JWF,NBAS,NBASF,IB,JB,KB,ITER,MSPN,MREP,IERR,I,I_OCC
INTEGER :: J,IFRAG,TNWF,TN_OCC,TNBASF,TOCC,FRAG_OFFSET,OFFSET1,BAS_OFFSET
INTEGER,DIMENSION(NSPN) :: TNWFS
REAL*8  :: ERR,ER1,ER2,DOT
REAL*8,ALLOCATABLE :: TEMP(:)
!
! CHECK SIZES OF FRAGMENTS TO ALLOCATE PSI_COEF
!
WRITE(6,*)'READING WAEFUNCTIONS IN FRAGMENTS'
WRITE(6,*)'FRAGMENTS=',NFRAGMENT
NBASF=0
NWF=0
DO IFRAG=1,NFRAGMENT
  WRITE(WFFILE2,'(A,I2.2)')TRIM(WFFILE),IFRAG
  INQUIRE(FILE=WFFILE2,EXIST=EXIST)
  IF(EXIST)THEN
    OPEN(99,FILE=WFFILE2,FORM='UNFORMATTED',STATUS='UNKNOWN')
    READ(99,END=900) MSPN
    IF (MSPN.NE.NSPN) GOTO 900
    IF (NSPN.GT.MXSPN) THEN
      WRITE(6,*)'READWF: NSPN > MXSPN'
      CALL STOPIT
    END IF
    READ(99,END=900) TNWF,(TNWFS(ISPN), ISPN=1,NSPN)
    NWF=NWF+TNWF
    DO J=1,NSPN
      NWFS(J)=NWFS(J)+TNWFS(J)
    ENDDO
    READ(99,END=900)(EVLOCC(IWF), IWF=1,TNWF)
    ITOT=0
    READ(99,END=900) MREP
    IF(MREP.NE.N_REP) GOTO 900
    DO ISPN=1,NSPN
      DO IREP=1,N_REP
        READ(99,END=900) TN_OCC,TNBASF
        NBASF=NBASF+TNBASF
        READ(99,END=900)(OCCUPANCY(I_OCC+ITOT),I_OCC=1,N_OCC(IREP,ISPN))
        ITOT=ITOT+TN_OCC
        ALLOCATE(TEMP(TNBASF))
        DO IWF=1,TN_OCC
          READ(99,END=900)(TEMP(I),I=1,TNBASF)
        END DO
        DEALLOCATE(TEMP)
      END DO
    ENDDO
    CLOSE(99)
  ELSE
    WRITE(6,*)'READWF_FRAG:ERROR WAVEFUNCTION FILE NOT FOUND',WFFILE2
    CALL STOPIT
  ENDIF
ENDDO
IF (NWF.GT.MAX_OCC) THEN
  write(6,*)'READWF: MAX_OCC MUST BE AT LEAST: ',NWF
  CALL STOPIT
END IF
!
! ZERO PSI_COEF
!
DO IREP=1,N_REP
  DO ISPN=1,NSPN
    DO I=1,NBASF
      DO J=1,NWF
        PSI_COEF(J,I,IREP,ISPN)=0.0D0
      ENDDO
    ENDDO
  ENDDO
ENDDO
!
! READ IN ATOMIC LOCATIONS AND BASIS SET INFORMATION
!
FAILED= .FALSE.

FRAG_OFFSET=0
OFFSET1=0
BAS_OFFSET=0
DO IFRAG=1,NFRAGMENT
  WRITE(WFFILE2,'(A,I2.2)')TRIM(WFFILE),IFRAG
  PRINT '(2A)','READING OLD WAVEFUNCTIONS FROM FILE ',WFFILE2
  OPEN(99,FILE=WFFILE2,FORM='UNFORMATTED',STATUS='UNKNOWN')
  READ(99,END=900) MSPN
  IF (MSPN.NE.NSPN) GOTO 900
  IF (NSPN.GT.MXSPN) THEN
    write(6,*)'READWF: NSPN > MXSPN'
    CALL STOPIT
  END IF
  READ(99,END=900) TNWF,(TNWFS(ISPN), ISPN=1,NSPN)
  READ(99,END=900)(EVLOCC(IWF+FRAG_OFFSET), IWF=1,TNWF)
  ITOT=0
  READ(99,END=900) MREP
  IF (MREP.NE.N_REP) GOTO 900
  DO ISPN=1,NSPN
    DO IREP=1,N_REP
      READ(99,END=900) TN_OCC,TNBASF
      IF (NBASF.NE.NS_TOT(IREP)) GOTO 900
      READ(99,END=900)(OCCUPANCY(I_OCC+ITOT+OFFSET1),I_OCC=1,TN_OCC)
      ITOT=ITOT+TN_OCC
!      WRITE(6,*)'PSI_COEF FROM',1+FRAG_OFFSET,1+BAS_OFFSET
!      WRITE(6,*)'TO',TN_OCC+FRAG_OFFSET,TNBASF+BAS_OFFSET
      DO IWF=1,TN_OCC
        READ(99,END=900)(PSI_COEF(I+BAS_OFFSET,IWF+FRAG_OFFSET,IREP,ISPN),I=1,TNBASF)
      END DO
      N_OCC(IREP,ISPN)=N_OCC(IREP,ISPN)+TN_OCC
    END DO
  ENDDO
  FRAG_OFFSET=FRAG_OFFSET+TNWF
  OFFSET1=TNWF
  BAS_OFFSET=BAS_OFFSET+TNBASF
  CLOSE(99)
ENDDO
!
! REORTHONORMALIZE OCCUPIED WAVEFUNCTIONS:
!
IF (DEBUG) write(6,*)'READWF CALLS OVERLAP MODE: 1'
CALL OVERLAP(1)
DO ISPN=1,NSPN
  KB=0
  DO IREP=1,N_REP
    NBAS=NS_TOT(IREP)
    ALLOCATE(AHAM(NBAS,NBAS),STAT=IERR)
    IF(IERR.NE.0)THEN
      WRITE(6,*)'readwf:Error allocating Ham'
    ENDIF
    ALLOCATE(AOVER(NBAS,NBAS),STAT=IERR)
    IF(IERR.NE.0)THEN
      WRITE(6,*)'readwf:Error allocating Overlap'
    ENDIF
    DO IB=1 ,NBAS
      DO JB=IB,NBAS
        KB=KB+1
        AOVER(JB,IB)=HSTOR(KB,1)
        AOVER(IB,JB)=HSTOR(KB,1)
      ENDDO
    ENDDO
    ERR=0.0D0
    DO IWF=1,N_OCC(IREP,ISPN)
      DO JWF=1,IWF
        DOT=0.0D0
        IF (JWF.EQ.IWF) THEN
          DO IB=1,NBAS
            DO JB=1,NBAS
              DOT=DOT+AOVER(JB,IB)*PSI_COEF(JB,JWF,IREP,ISPN)*PSI_COEF(IB,IWF,IREP,ISPN)
            END DO
          END DO
          ERR=ERR+ABS(DOT-1.0D0)
          DO IB=1,NBAS
            PSI_COEF(IB,IWF,IREP,ISPN)=PSI_COEF(IB,IWF,IREP,ISPN)/SQRT(DOT)
          END DO 
          DO IB=1,NBAS
            AHAM(IB,IWF)=0.0D0
            DO JB=1,NBAS
              AHAM(IB,IWF)=AHAM(IB,IWF)+PSI_COEF(JB,IWF,IREP,ISPN)*AOVER(JB,IB)
            END DO
          END DO
        ELSE
          DOT=0.0D0
          DO IB=1,NBAS
           DOT=DOT+PSI_COEF(IB,IWF,IREP,ISPN)*AHAM(IB,JWF)
          END DO
          ERR=ERR+ABS(DOT)
          DO IB=1,NBAS
           PSI_COEF(IB,IWF,IREP,ISPN)=PSI_COEF(IB,IWF,IREP,ISPN)-PSI_COEF(IB,JWF,IREP,ISPN)*DOT      
          END DO
        END IF
      ENDDO
    ENDDO
    ER1=ERR
!
! TEST
!
    IF(DEBUG) THEN
      ERR=0.0D0
      DO IWF=1,N_OCC(IREP,ISPN)
        DO JWF=1,IWF
          DOT=0.0D0
          DO IB=1,NBAS
            DO JB=1,NBAS
              DOT=DOT+AOVER(JB,IB)*PSI_COEF(JB,JWF,IREP,ISPN)*PSI_COEF(IB,IWF,IREP,ISPN)
            END DO
          END DO
          IF (IWF.EQ.JWF) THEN
            ERR=ERR+ABS(DOT-1.0D0)
          ELSE
            ERR=ERR+ABS(DOT)
          END IF
        ENDDO
      ENDDO
      write(6,*)'REORTHOGONALIZATION ERROR:',ERR,ER1
    ENDIF
    DEALLOCATE(AHAM,STAT=IERR)
    IF(IERR.NE.0)THEN
      WRITE(6,*)'readwf:Error deallocating Ham'
    ENDIF
    DEALLOCATE(AOVER,STAT=IERR)
    IF(IERR.NE.0)THEN
      WRITE(6,*)'readwf:Error deallocating Overlap'
    ENDIF
  ENDDO
ENDDO
!
! REMOVE OLD EVALUE FILES AND CREATE DUMMY FILE FOR FIRST ITERATION
!
    ITER=0
300 ITER=ITER+1
    WRITE(EVALSTR,'(A,I3.3)')'EVAL',ITER
    INQUIRE(FILE=EVALSTR,EXIST=EXIST)
    IF (EXIST) THEN
      OPEN(98,FILE=EVALSTR,FORM='FORMATTED',STATUS='OLD')
      CLOSE(98,STATUS='DELETE')
      GOTO 300
    END IF
    CONTINUE
    OPEN(98,FILE='EVAL001',FORM='FORMATTED',STATUS='UNKNOWN')
    REWIND(98)
    WRITE(98,*) 'NO EIGENVALUES FOR READ WAVEFUNCTIONS'  
    CLOSE(98)
    RETURN
!      
! FAILURE
!
900 PRINT '(A)','UNABLE TO READ OR PROCESS OLD WAVEFUNCTIONS'
    PRINT '(A)','PROCEEDING WITH DEFAULT STARTING POINT'
    FAILED= .TRUE.
    RETURN
END SUBROUTINE READWF_FRAG
