C UTEP Electronic Structure Lab (2020)
       SUBROUTINE HYPERF(ISTEP)
C
C * HYPERFINE FIELD DUE TO FERMI CONTACT TERM
C   USING THOMSON RADIUS R=Z e^2 / m c^2      PRB 35,3721
C   JK 09/2000
C
C ANGULAR DECOMPOSITION FOR WAVEFUNCTIONS       
C
       use mesh1,only : wmsh,rmsh,nmsh
       use debug1
       use common2,only : RIDT, IFUIDT, NIDENT, ZNUC, N_CON, LSYMMAX,
     &   N_POS, NFNCT, ISPN, NSPN, SYMATM
       use common3,only : RMAT
       use common5,only : PSI, NWF, NWFS
! Conversion to implicit none.  Raja Zope Thu Aug 17 14:34:49 MDT 2017

!      INCLUDE  'PARAMAS'  
       INCLUDE  'PARAMA2'  
       INTEGER :: LMXX, ISTEP, MAXANG, MAXSPH, I, I_POS, IANG, IATOM,
     & ICON, IFNCT, IH, ILOC, IPTS, IR, ISHDUMMY, ISHELLA, ISIZE,
     & ISPHERE, ISWITCH, IWF, J, J_POS, JPTS, JWF, L, L_NUC, LI, LMAX,
     & LMX1, LMX2, LPBEG, LPTS, LPV, LSIZ, M_NUC, MAXRAD, MCALC, MPTS,
     & MSITES, MTOT, MU, MXCHG, NANG, NMAX, NPOL, NPOW, NPV, NRAD,
     & NSPHERE
       REAL*8 :: SYMBOL , AFUDGE, AMAX, AMIN, ANGLE, AU2ANG, CENTER,
     & CLIGHT, DOMEGA, DOS, EMN, EMX, ERRMAX, EXACT, FACTOR, FAK, GRAD,
     & HFF, PI, PSIG, PSIL, PTS, QL, QLDS, QTOT, RANG, RHOG, RMAX,
     & RMIN, RVECA, SPN, TIME1, TIME2, VOL, WTRAD, XRAD, YLM, Z
       SAVE
       PARAMETER (NMAX=MPBLOCK)
       PARAMETER (LMXX=20)
       PARAMETER (LSIZ=(LMXX+1)**2)
       PARAMETER (MAXANG=((2*LMXX+1)*(LMXX+1)))
       PARAMETER (MAXSPH=500)
       PARAMETER (MAXRAD=1000)
       PARAMETER (MXCHG=56)
       CHARACTER*80 LINE
C
       LOGICAL LMKFIL,EXIST
       LOGICAL IUPDAT,ICOUNT
C
C SCRATCH COMMON BLOCK FOR LOCAL ARRAYS
C
       COMMON/TMP1/PTS(NSPEED,3),PSIL(MAX_OCC,LSIZ,2)
     &  ,RANG(3,MAXANG),QL(LMXX+2),QTOT(LMXX+2),DOS(LMXX+2)
     &  ,QLDS(LMXX+2,MAX_OCC),YLM(MAXANG,LSIZ)
     &  ,EMN(MAXSPH),EMX(MAXSPH),XRAD(MAXRAD),WTRAD(MAXRAD)
     &  ,CENTER(6,MAXSPH),ANGLE(3,MAXANG),DOMEGA(MAXANG)
     &  ,RVECA(3,MX_GRP),GRAD(NSPEED,10,6,MAX_CON,3)
     &  ,ICOUNT(MAX_CON,3)
       COMMON/TMP2/PSIG(NMAX,MAX_OCC),RHOG(MAX_PTS,KRHOG,MXSPN)
C
       DIMENSION ISIZE(3),MSITES(1),SPN(2)
       REAL*8 VDW(MXCHG),RKOV(MXCHG)
       CHARACTER*12 ATOMSTR 
       DATA ISIZE/1,3,6/
       DATA AU2ANG/0.529177D0/
       DATA CLIGHT/137.088D0/
C
C RETURN IF INPUT FILE DOES NOT EXIST
C
        IF(NSPN.EQ.1) RETURN
        PRINT '(A)','CALCULATING HYPERFINE FIELD - FERMI CONTACT'
        INQUIRE(FILE='HYPERFINE',EXIST=EXIST)
        IF (.NOT.EXIST) THEN
         PRINT '(2A)','HYPERFINE: FILE HYPERFINE DOES NOT EXIST ',
     &                '--> NOTHING TO DO'
         RETURN
        END IF
C
C CREATE A STANDARD INPUT FILE IF THE CURRENT INPUT FILE IS EMPTY 
C
        CALL GTTIME(TIME1)
        PI= 4*ATAN(1.0D0)
        LMKFIL=.TRUE.
        OPEN(74,FILE='HYPERFINE',FORM='FORMATTED',STATUS='OLD')
        REWIND(74)
        READ(74,*,END=5,ERR=5) ISWITCH
        IF (ISWITCH .NE. 0) LMKFIL=.FALSE.
    5   CLOSE(74)
C
        IF (LMKFIL) THEN
         OPEN(74,FILE='HYPERFINE',FORM='FORMATTED',STATUS='OLD')
         REWIND(74)
         WRITE(74,*) '0  auto=0, otherwise user-defined'
         WRITE(74,*) '1.0D-5 0.02 5 2  ERR,ALPMIN,LMAX,NPOW'
         WRITE(74,1010) NIDENT
 1010    FORMAT(' ',I5,' NUMBER OF CENTERS')
         DO IATOM=1,NIDENT
          DO J=1,3
           CENTER(J,IATOM)=RIDT(J,IATOM)
          END DO
          Z=ZNUC(IFUIDT(IATOM))
          CENTER(6,IATOM)=200*ABS(Z)**3
          CENTER(4,IATOM)=0.0D0
          CENTER(5,IATOM)=ABS(Z)/CLIGHT**2
C
          WRITE(74,1022)(CENTER(J,IATOM),J=1,6),(SYMATM(L,IATOM),L=1,10)
 1022     FORMAT(4(1X,F10.5),1X,E12.4,1X,E10.2,2X,10A)
         END DO
         CLOSE(74)
        END IF
C
C READ INPUT FILE
C CENTER CONTAINS THE COORDINATES (X,Y,Z), THE RADII R1,R2 AND THE
C MAXIMUM ALPHA FOR EACH CENTER
C
        OPEN(74,FILE='HYPERFINE',FORM='FORMATTED',STATUS='OLD')
        REWIND(74)
        READ(74,*,END=10) ISWITCH
        READ(74,*,END=10) ERRMAX,AMIN,LMAX,NPOW
        READ(74,*,END=10) NSPHERE
        IF (NSPHERE.GT.MAXSPH) THEN
         PRINT *,'HYPERFINE: MAXSPH MUST BE AT LEAST: ',NSPHERE
         GOTO 20
        END IF
        DO I=1,NSPHERE
         READ(74,33) LINE
         READ(LINE,*)(CENTER(J,I),J=1,6)
         DO J=80,1,-1
          IF (LINE(J:J).NE.' ') THEN 
           DO L=1,10
            IH=J-10+L
            SYMATM(L,I)=LINE(IH:IH)
           ENDDO
           GOTO 13
          ENDIF
         ENDDO
   13   CONTINUE 
        END DO
        GOTO 30
   10   PRINT *,'HYPERFINE: INPUT FILE IS INVALID'
   20   CLOSE(74) 
        GOTO 900
   30   CONTINUE
   33   FORMAT(A80)
C
C JK temp only : write results in extra files
C        CLOSE(74)
C        WRITE(ATOMSTR,'(A,I2.2)')'HYPERFI',ISTEP
C        OPEN(74,FILE=ATOMSTR,FORM='FORMATTED',STATUS='UNKNOWN')
C temp end
         WRITE(74,*)
         WRITE(74,'(A7,10X,A,10X,A,12X,A)')
     &    'CENTER ','R-THOMSON','SPIN-DENSITY','HFF (kGAUSS)'
C
C LMAX, LMX2 CHECK AND SETUP
C
        IF (LMAX.GT.10) THEN
         LMAX=10
         PRINT '(A)','HYPERFINE: WARNING: LMAX HAS BEEN REDUCED TO 10'
        END IF
        LMX2=LMXX ! (2*LMAX) USUALLY OK...
C
C SETUP ANGULAR POINTS AND SPHERICAL HARMONICS
C FIRST, CHECK IF ENOUGH SPACE IN YLM
C
        MPTS=0
        IF (DEBUG) PRINT *,'MAXANG IN HYPERFINE: ',MAXANG
        CALL HARMONICS(MAXANG,MPTS,LMAX,ANGLE,YLM,NPOL)
        IF (NPOL.GT.LSIZ) THEN
         PRINT *,'DECOMP: LSIZ MUST BE AT LEAST: ',NPOL
         CALL STOPIT
        END IF
C
C CALL ANGMSH TO GET ANGULAR POINTS. THEN, SEND THESE POINTS TO
C HARMONICS AND GET SPHERICAL HARMONICS
C
        CALL ANGMSH(MAXANG,LMX2,NANG,ANGLE,DOMEGA)
        IF (DEBUG) PRINT *,'DECOMP-ANGMSH: LMAX,NANG: ',LMX2,NANG
C
C LOOP FOR EACH SPHERE
C START BY SETTING UP RADIAL POINTS
C
        DO 850 ISPHERE=1,NSPHERE
         CALL GASITES(1,CENTER(1,ISPHERE),MTOT,RVECA,MSITES)
         RMIN=CENTER(4,ISPHERE)
         RMAX=CENTER(5,ISPHERE)
         AMAX=CENTER(6,ISPHERE)
         AFUDGE=1.2D0
          CALL RADMSH(MAXRAD,RMIN,RMAX,ERRMAX,AMIN,AMAX,AFUDGE,NPOW,
     &                NRAD,XRAD,WTRAD)
          NMSH=NRAD*NANG
          IF (NMSH .GT. MAX_PTS) THEN
           PRINT *,'ANALYZE: MAX_PTS EXCEEDED, NMSH: ',NMSH
           PRINT *,'SKIPPING ATOM SPHERE ',ISPHERE
           GOTO 850
          END IF
         PRINT '(A,I5,A,I5,A,F15.5)','SPHERE ',ISPHERE,
     &         ': ',NRAD*NANG,' POINTS, RADIUS= ',RMAX
C
C RADIAL INTEGRALS ARE STORED IN PSIL(IWF,LM,2)
C
C CALCULATE CHARGE DENSITIES AT EACH POINT IN SPACE:
C FOR RADIAL SPHERES:
         NMSH=0
         VOL=0
         DO IR=1,NRAD
          DO IANG=1,NANG
          NMSH=NMSH+1
           RMSH(1,NMSH)=XRAD(IR)*ANGLE(1,IANG)+CENTER(1,ISPHERE)
           RMSH(2,NMSH)=XRAD(IR)*ANGLE(2,IANG)+CENTER(2,ISPHERE)
           RMSH(3,NMSH)=XRAD(IR)*ANGLE(3,IANG)+CENTER(3,ISPHERE)
           WMSH(NMSH)=WTRAD(IR)*DOMEGA(IANG)
           VOL=VOL+WMSH(NMSH)
          END DO
         END DO
          EXACT=(4.0D0/3.0D0)*PI*RMAX**3
          IF (DEBUG) PRINT *,'TOTAL NUMBER OF POINTS: ',NMSH
          IF (DEBUG) PRINT *,'TOTAL VOLUME: ',VOL,' EXACT: ',EXACT
C
           DO ISPN=1,NSPN
            DO IPTS=1,NMSH
             RHOG(IPTS,ISPN,1)=0.0D0
            END DO
           END DO
C
C POINTS LOOP
C
         LPTS=0
   40    CONTINUE
          MPTS=MIN(NMAX,NMSH-LPTS)
          LPBEG=LPTS
C
C INITIALIZE PSIG 
C
          DO IWF=1,NWF
           DO IPTS=1,MPTS
            PSIG(IPTS,IWF)=0.0D0
           END DO  
          END DO  
          ISHELLA=0
          DO 86 IFNCT=1,NFNCT
           LMX1=LSYMMAX(IFNCT)+1
           DO 84 I_POS=1,N_POS(IFNCT)
            ISHELLA=ISHELLA+1
            CALL OBINFO(1,RIDT(1,ISHELLA),RVECA,M_NUC,ISHDUMMY)
            DO 82 J_POS=1,M_NUC
             CALL UNRAVEL(IFNCT,ISHELLA,J_POS,RIDT(1,ISHELLA),
     &                    RVECA,L_NUC,1)
             IF(L_NUC.NE.M_NUC)THEN
              PRINT *,'DECOMP: PROBLEM IN UNRAVEL'
              CALL STOPIT 
             END IF
             LPTS=LPBEG
             DO 80 JPTS=1,MPTS,NSPEED
              NPV=MIN(NSPEED,MPTS-JPTS+1)
              DO LPV=1,NPV
               PTS(LPV,1)=RMSH(1,LPTS+LPV)-RVECA(1,J_POS)
               PTS(LPV,2)=RMSH(2,LPTS+LPV)-RVECA(2,J_POS)
               PTS(LPV,3)=RMSH(3,LPTS+LPV)-RVECA(3,J_POS)
              END DO
C
C GET ORBITS AND DERIVATIVES
C
              CALL GORBDRV(0,IUPDAT,ICOUNT,NPV,PTS,IFNCT,GRAD)
C
C UPDATING ARRAY PSIG
C
              IF (IUPDAT) THEN
               IPTS=JPTS-1
               ILOC=0
               DO 78 LI=1,LMX1
                DO MU=1,ISIZE(LI)
                 DO ICON=1,N_CON(LI,IFNCT)
                  ILOC=ILOC+1
                  IF (ICOUNT(ICON,LI)) THEN
                   DO IWF=1,NWF
                    FACTOR=PSI(ILOC,IWF,1)
                    DO LPV=1,NPV
                     PSIG(IPTS+LPV,IWF)=PSIG(IPTS+LPV,IWF)
     &               +FACTOR*GRAD(LPV,1,MU,ICON,LI)
                    END DO
                   END DO  
                  END IF
                 END DO  
                END DO  
   78          CONTINUE
              END IF
              LPTS=LPTS+NPV
   80        CONTINUE
   82       CONTINUE
   84      CONTINUE
   86     CONTINUE
C
C UPDATE CHARGE DENSITY:
C
           IWF=0
           DO ISPN=1,NSPN
           DO JWF=1,NWFS(ISPN)
           IWF=IWF+1
            DO IPTS=1,MPTS
             RHOG(IPTS+LPBEG,ISPN,1)=
     &       RHOG(IPTS+LPBEG,ISPN,1)+PSIG(IPTS,IWF)**2
            END DO
           END DO
           END DO
C
C CHECK IF ALL POINTS DONE
C
          LPTS=LPBEG+MPTS
          IF(LPTS.GT.MAX_PTS)THEN
           PRINT *,'DECOMP: ERROR: LPTS >',MAX_PTS
           CALL STOPIT
          ELSE
           IF(LPTS.LT.NMSH) GOTO 40
          END IF
  500    CONTINUE
C ANALYSE SPIN DENSITIES:
          DO ISPN=1,NSPN
          SPN(ISPN)=0.0D0
          DO IPTS=1,NMSH
           SPN(ISPN)=SPN(ISPN)+RHOG(IPTS,ISPN,1)*WMSH(IPTS)
          END DO
          END DO
C          DNS=SPN(1)
C          RMN=SPN(NSPN)-SPN(1) 
          SPN(1)=SPN(1)/EXACT
          SPN(2)=SPN(2)/EXACT
C Bhf=-2/3 mu_0 mu_B m_rt
C m_rt=spin denisty averaged over Thomson radius
C FAK =-8/3 pi 9.274 /(0.529177)**3 *10    !kGauss          
          FAK=-524.3D0    !kGAUSS  
          HFF=(SPN(2)-SPN(1))* FAK
          WRITE(74,100)ISPHERE,(SYMATM(L,ISPHERE),L=1,10),
     &               CENTER(5,ISPHERE),(SPN(ISPN),ISPN=1,NSPN),HFF
 100      FORMAT(I3,2X,10A,F10.6,2F16.5,3X,F10.3) 
  850   CONTINUE
        CLOSE(74)
  900   CONTINUE
C REREAD MESH... (ATOMSPH OVERWROTE IT...)
          OPEN(99,FILE='VMOLD',FORM='UNFORMATTED',STATUS='UNKNOWN')
          READ(99)NMSH,MCALC
          READ(99)((RMSH(J,I),J=1,3),I=1,NMSH)
          READ(99)(WMSH(I),I=1,NMSH)
          CLOSE(99)
C END OF REREADING                       
        CALL GTTIME(TIME2)
        CALL TIMOUT('CHARGES IN ATOMIC SPHERES:         ',TIME2-TIME1)
        RETURN
       END
